// project is Maven-style
apply plugin: 'groovy'
apply plugin: 'maven-publish'
apply plugin: 'signing'

// eclipse project generation support
apply plugin: 'eclipse'

// identifiers
//group = 'com.github.csabasulyok'
group = 'org.jvoicexml'
version = '2.0-SNAPSHOT'


dependencies {
  // make this a gradle plugin project
  compile gradleApi()
  implementation localGroovy()
  
  // testing
  testCompile group:'junit', name:'junit', version:'4.12'
}


// use main Maven repos
repositories {
  mavenLocal()
  mavenCentral()
}


// print test report (for Travis)
test {
  afterTest { desc, result ->
    println "  Test ${desc.className}.${desc.name} - ${result.resultType}"
  }
}


// extra eclipse setup
eclipse {
  // do not hardcode Maven repo in project file
  pathVariables 'M2_REPO': file(repositories[0].url)
}


/**
 * define extra tasks to package sources/test classes/javadoc and add to build lifecycle
 */
task sources(type: Jar, group: 'Build', description: 'Package sources', dependsOn: 'classes') {
  classifier = 'sources'
  from sourceSets.main.groovy
}

task testJar(type: Jar, group: 'Build', description: 'Assemble JAR archive with test classes', dependsOn: 'testClasses') {
  classifier = 'tests'
  from sourceSets.test.output
}

task packageJavadoc(type: Jar, group: 'Build', description: 'Packages output of groovydoc', dependsOn: 'groovydoc') {
  classifier = 'javadoc'
  from tasks.groovydoc.destinationDir
}

artifacts {
  archives tasks.sources
  archives tasks.testJar
  archives tasks.packageJavadoc
}


/**
 * configure upload of the artifacts
 */
// signing setup
signing {
  sign configurations.archives
}

publishing {
  repositories {

    maven{
      def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
      def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/" 
      url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
      credentials {
        username ossrhUsername
        password ossrhPassword
      }
    }
  }

  publications {
    mavenJava(MavenPublication) {
      from components.java

      pom {
        name = 'Gradle LaTeX plugin'
        description = 'This Gradle plugin allows automation of compiling PDFs using LaTeX and BiBTeX.'
        url = 'https://github.com/csabasulyok/gradle-latex'

        licenses {
          license {
            name = 'The Apache License, Version 2.0'
            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }

        scm {
          connection = 'scm:git:git://github.com/csabasulyok/gradle-latex.git'
          developerConnection = 'scm:git:ssh://github.com:csabasulyok/gradle-latex.git'
          url = 'https://github.com/csabasulyok/gradle-latex'
        }

        developers {
          developer {
            id = 'csabasulyok'
            name = 'Csaba Sulyok'
            email = 'csaba.sulyok@gmail.com'
          }
        }
      }
    }
  }
}